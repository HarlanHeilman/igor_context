#pragma rtGlobals=3		// Use modern global access method.
#pragma modulename=GizmoContours
#pragma version=6.3		// shipped with Igor 6.30B03
#pragma IgorVersion=6.2	// for #pragma rtGlobals=3
#include <Extract Contours as Waves>, version>= 6.13, menus=0
#include <GizmoUtils>, version>=6.2	// for TopGizmo, etc.

// AppendContourToGizmo.ipf
//
// Creates contours from a Gizmo's surface data wave and appends it to the Gizmo window.
// The contours are composed of multiple Gizmo ribbon or path objects named "contour00Lev00", "contour001Lev03", etc.
//
// When the contours are plotted on the contour's surface, they're created as vertical Gizmo Ribbons
// with some height to keep them from sinking below the surface.
// Since the ribbons are vertical, when viewed directly along the Z axis they disappear.
// In that case, create the contours on the XY plane, described next.
//
// When the contours are plotted on an XY plane, they're created as paths.
//
// Currently the contours are generated by a contour plot in a briefly-appearing graph window.
//
// 5.04: Feb 18, 2005 - Initial Version, Jim Prouty, WaveMetrics, Inc, requires Igor 5.04 for KillDataFolder/Z
// 5.05: April 3, 2006 - added emission lighting to path contours (to avoid all-black contour lines)
// 6.01: February 19, 2007
//		- Removed colorMaterial from non-ribbon contours. The emissive color material was designed to help
//		the colors show up in low light situations, so if they're black instead of colored,
//		add some non-black ambient light.
// 6.02: August 16, 2007
//		- ModifyPanel noEdit, used named Gizmo hook function instead of unnamed Gizmo hook.
// 6.11: July 22, 2009
//		- fixed PopupMenu contoursOfSurface and PopupMenu surfaces to work in independent modules.
// 6.13: November 13, 2009
//		- removed menu access to Extract Contours as Waves; it's confusing, and not Gizmo-related.
// 6.2: May 2010:
//		- Used public versions of generally useful routines in GizmoUtils.ipf
//		- NewContour now honors the surface's before and after colors.
//		- adjusted the contour colors slightly. Requires Igor 6.2.
// 6.3: October 2012:
//		- Added test for how stale the contours are relative to the surface data.

// Main public routine:
Function WMGizmoContourPanel()

	DoWindow/F GizmoContours
	if( V_Flag == 0 )
	// Init data folders
		String oldDF= SetPanelDF()
		SetDataFolder oldDF

	// Create the panel
		NewPanel/N=GizmoContours/W=(10,40,571,618)/K=1 as "Gizmo Contours"
		DoWindow/C GizmoContours	// in case a window macro somehow got saved
		ModifyPanel fixedSize=1, noEdit=1

		SetVariable topGizmoSV,pos={23,15},size={200,15},disable=2,title="Gizmo Window Name:"
		SetVariable topGizmoSV,frame=0,value= root:Packages:GizmoContours:topGizmo
		
		String gizmoName= TopGizmo()
		if( strlen(gizmoName) )
			AutoPositionWindow/E/R=$gizmoName GizmoContours
		endif

	// Surfaces
		GroupBox surfaceGroup,pos={12,35},size={539,502},title="                                  "
		PopupMenu surfaces,pos={28,33},size={158,20},proc=GizmoContours#MatrixSurfacesPopMenuProc,title="Matrix Surfaces:"
		String cmd= GetIndependentModuleName()+"#GizmoContours#GetGizmoMatrixSurfaceList(\"\")"
		PopupMenu surfaces,mode=1,popvalue="surface0",value= #cmd

		SetVariable pathToSurfaceData,pos={28,61},size={398,15},disable=2,title="Surface Data Wave:"
		SetVariable pathToSurfaceData,frame=0
		SetVariable pathToSurfaceData,value= root:Packages:GizmoContours:pathToSurfaceData

	// Contours
		PopupMenu contoursOfSurface,pos={28,82},size={160,20},proc=GizmoContours#ContoursPopMenuProc,title="Contours of surface0"
		PopupMenu contoursOfSurface,mode=1,popvalue="_new_"
		Button removeContour,pos={244,83},size={70,20},disable=2,proc=GizmoContours#RemoveContourButtonProc,title="Remove"

	// Levels
		GroupBox levelsGroup,pos={29,109},size={510,95},title="Contour Levels"
		CheckBox radioAutoLevels,pos={50,135},size={107,14},proc=GizmoContours#ContourLevelsRadioProc,title="Approx Auto Levels"
		CheckBox radioAutoLevels,mode=1	// ,variable= root:Packages:GizmoContours:Gizmo0:surface0:Nascent:useAutoLevels

		SetVariable numAutoLevels,pos={163,135},size={50,15},proc=GizmoContours#NumAutoLevelsSetVarProc,title=" "
		SetVariable numAutoLevels,limits={1,1000,1}	// value= root:Packages:GizmoContours:Gizmo0:surface0:Nascent:numAutoLevels

		Button presetLevelsList,pos={238,130},size={263,20},proc=GizmoContours#SetLevelsListToAutoLevelsButton,title="\\W623Set List of Levels to Auto Levels \\W623"

		CheckBox radioListOfLevels,pos={50,162},size={79,14},proc=GizmoContours#ContourLevelsRadioProc,title="List of Levels"
		CheckBox radioListOfLevels,value= 0,mode=1

		SetVariable listOfLevels,pos={140,162},size={382,15},proc=GizmoContours#LevelsListSetVarProc,title=" "
		// SetVariable listOfLevels,value= root:Packages:GizmoContours:Gizmo0:surface0:Nascent:listOfLevels

		TitleBox levelsListError,pos={80,185},size={1,1},frame=0
		// TitleBox levelsListError,variable= root:Packages:GizmoContours:Gizmo0:surface0:Nascent:listOfLevels

	// Displayed Where
		// Ribbons
		CheckBox contoursOnSurface,pos={32,217},size={145,14},proc=GizmoContours#DisplayContoursRadioProc,title="Display Contours on Surface"
		CheckBox contoursOnSurface,value=0,mode=1

		SetVariable ribbonHeight,pos={217,217},size={220,15},proc=GizmoContours#SetVarProc,title="Ribbon Height (% of Z Max- Z Min)"
		SetVariable ribbonHeight,limits={0,10,0.25} // value= root:Packages:GizmoContours:Gizmo0:surface0:Nascent:ribbonHeight

		CheckBox calcNormals,pos={455,217},size={121,14},proc=GizmoContours#CalcNormalsCheckProc,title="Calc Normals",value= 0
	
		// Paths
		GroupBox whereDivider,pos={51,236},size={484,4}

		CheckBox contoursAtMaxZ,pos={32,242},size={245,14},proc=GizmoContours#DisplayContoursRadioProc,title="Display Contours on XY plane at Z = Surface Max Z"
		CheckBox contoursAtMaxZ,value= 0,mode=1
		
		CheckBox contoursAtSpecificZ,pos={32,267},size={178,14},proc=GizmoContours#DisplayContoursRadioProc,title="Display Contours on XY Plane at Z ="
		CheckBox contoursAtSpecificZ,value= 0,mode=1
		SetVariable displayContourZLevel,pos={227,267},size={80,15},proc=GizmoContours#DisplayContoursAtZSetVarProc,title=" "
		SetVariable displayContourZLevel,limits={-inf,inf,0} // value= root:Packages:GizmoContours:Gizmo0:surface0:Nascent:displayContourZLevel

		SetVariable pathLineSize,pos={332,242},size={130,15},disable=1,proc=GizmoContours#SetVarProc,title="Line Size (points)"
		SetVariable pathLineSize,limits={1,10,1} // value= root:Packages:GizmoContours:Gizmo0:surface0:Nascent:pathLineSize

		CheckBox contoursAtMinZ,pos={32,293},size={242,14},proc=GizmoContours#DisplayContoursRadioProc,title="Display Contours on XY plane at Z = Surface Min Z"
		CheckBox contoursAtMinZ,value= 1,mode=1
		
	// Color Plane stuff
	
		CheckBox displayColorPlane,pos={332,267},size={105,14},proc=GizmoContours#DisplayColorPlaneCheckProc,title="Display Color Plane"

		// Plane color
		PopupMenu colorPlaneColor,pos={446,265},size={50,20},proc=GizmoContours#ColorPlaneColorPopMenuProc
		PopupMenu colorPlaneColor,mode=30,popColor= (65535,0,0),value= #"\"*COLORPOP*\""

		// Plane Below/Above
		CheckBox radioPlaneBelowContours, win= GizmoContours, value= 1, mode=1, pos={350,290},size={83,14},proc=GizmoContours#PlaneWhereRadioProc,title="Below Contour"
		CheckBox radioPlaneAboveContours, win= GizmoContours, value= 0, mode=1, pos={440,290},size={84,14},proc=GizmoContours#PlaneWhereRadioProc,title="Above Contour"

		// Plane Z Offset (% z Range)
		SetVariable planeZOffsetPct,pos={368,309},size={151,15},proc=GizmoContours#SetVarProc,title="Offset by % Z  Range"
		SetVariable planeZOffsetPct,limits={0,10,0.25}

	// Colors
		GroupBox colorsGroup,pos={29,320},size={508,168},title="Contour Colors"
		
		// Use Colors from Surface
		CheckBox radioColorFromSurface,pos={50,350},size={129,14},proc=GizmoContours#ContourColorsRadioProc,title="Use Colors from Surface",mode=1
		//CheckBox radioColorFromSurface,variable= root:Packages:GizmoContours:Gizmo3:surface0:contour00:useColorsFromSurface

		// Use Same Color
		CheckBox radioColorSame,pos={50,382},size={88,14},proc=GizmoContours#ContourColorsRadioProc,title="Use Same Color"
		CheckBox radioColorSame,value= 0,mode=1
		PopupMenu contourSameColorPop,pos={147,380},size={50,20},proc=GizmoContours#SameColorPopMenuProc
		PopupMenu contourSameColorPop,mode=30,popColor= (65535,0,0),value= #"\"*COLORPOP*\""

		// Use Color Table
		CheckBox radioColorFromTable,pos={50,415},size={88,14},proc=GizmoContours#ContourColorsRadioProc,title="Use Color Table"
		CheckBox radioColorFromTable,value= 0,mode=1
		PopupMenu contourColorPop,pos={147,413},size={200,20},proc=GizmoContours#ColorTablePopMenuProc
		PopupMenu contourColorPop,mode=2,popvalue="",value= #"\"*COLORTABLEPOP*\""
		CheckBox reverse,pos={364,416},size={87,14},proc=GizmoContours#ReverseCheckProc,title="Reverse Colors"
		// CheckBox reverse,variable= root:Packages:GizmoContours:Gizmo0:surface0:Nascent:reverseColorTable

			// First color
		CheckBox firstColorSurfaceMinZ,pos={91,442},size={144,14},proc=GizmoContours#FirstColorRadioProc,title="First Color at Surface Min Z",mode=1
		// CheckBox firstColorSurfaceMinZ ,variable= root:Packages:GizmoContours:Gizmo0:surface0:Nascent:firstColorAtMinZ

		CheckBox firstColorAt,pos={91,463},size={115,14},proc=GizmoContours#FirstColorRadioProc,title="First Color at Level ="
		CheckBox firstColorAt,value= 0,mode=1
		SetVariable firstColorZ,pos={216,463},size={80,15},proc=GizmoContours#FirstColorAtZSetVarProc,title=" "
		SetVariable firstColorZ,limits={-inf,inf,0} // value= root:Packages:GizmoContours:Gizmo0:surface0:Nascent:firstColorAtLevel

		GroupBox colorDivider,pos={303,440},size={4,40}

		CheckBox lastColorSurfaceMaxZ,pos={321,442},size={144,14},proc=GizmoContours#LastColorRadioProc,title="Last Color at Surface Max Z",mode=1
		// CheckBox lastColorSurfaceMaxZ,variable= root:Packages:GizmoContours:Gizmo0:surface0:Nascent:lastColorAtMaxZ

		CheckBox lastColorAt,pos={321,465},size={112,14},proc=GizmoContours#LastColorRadioProc,title="Last Color at Level ="
		CheckBox lastColorAt,value= 0,mode=1
		SetVariable lastColorZ,pos={442,464},size={80,15},title=" "
		SetVariable lastColorZ,limits={-inf,inf,0} // value= root:Packages:GizmoContours:Gizmo0:surface0:Nascent:lastColorAtLevel
		
	// Add/Update	
		CheckBox updateOnChange,pos={54,504},size={106,14},title="Update Immediately",value= 1
		Button createOrUpdateContour,pos={208,503},size={159,20},proc=GizmoContours#AddOrUpdateButtonProc,title="Append Contours"

	// Stuff
		Button help,pos={465,543},size={50,20},title="Help", proc=GizmoContours#HelpButtonProc
		
		Button gizmoInfo,pos={36,543},size={90,20},proc=GizmoContours#GizmoInfoButtonProc,title="Gizmo Info"

		SetWindow kwTopWin,hook(GizmoContours)=GizmoContours#WindowHook
		UpdateGizmoContourPanel()
	else
		// activate event will call UpdateGizmoContourPanel()
	endif
End


// returns gizmoName or ""
Static Function/S UpdateGizmoContourPanel()

	DoWindow GizmoContours
	if( V_Flag == 0 )
		return ""
	endif

	String allControls= ControlNameList("GizmoContours", ";", "*")
	allControls= RemoveFromList("topGizmoSV;help;", allControls)

	String gizmoName=TopGizmo()
	String/G $PanelDFVar("topGizmo")= gizmoName	// for the topGizmoSV SetVariable
	
	if( strlen(gizmoName) == 0 )
		ModifyControlList/Z allControls, win=GizmoContours, disable=1
		return ""	// NO GIZMO!
	else
		ModifyControlList/Z allControls, win=GizmoContours, disable=0
		UpdateHookFunction(gizmoName, "GizmoContours", "WMGizmoContoursNamedHook", "WMGizmoContoursHook")
	endif

	// transfer control settings from globals (or defaults) to controls

	// Surface popup and path to surface data wave
	Variable mode
	String surfaceName, pathToSurfaceData

	String surfaceNameList="", surfaceDataPathList=""
	Variable numSurfaces= GetGizmoSurfacesNotPlanes(gizmoName, 1, surfaceNameList, surfaceDataPathList)

	if( numSurfaces )
		surfaceName=  StrVarOrDefault(GizmoDFVar(gizmoName, "surfaceName"), StringFromList(0,surfaceNameList))
		Variable whichOne= max(0,WhichListItem(surfaceName, surfaceNameList))
		surfaceName= StringFromList(whichOne, surfaceNameList)
		pathToSurfaceData=StringFromList(whichOne, surfaceDataPathList)
		mode= 1+whichOne
	else
		surfaceName="_none_"
		surfaceNameList= "_none_"
		mode=1
		pathToSurfaceData="n/a"
	endif

	PopupMenu surfaces, win=GizmoContours, mode=mode, popvalue=surfaceName
	
	if( CmpStr(surfaceName,"_none_") == 0 )
		allControls= RemoveFromList("gizmoInfo;surfaceGroup;surfaces;", allControls)
		ModifyControlList/Z allControls, win=GizmoContours, disable=1	// hide other controls
		PopupMenu surfaces, win=GizmoContours, disable=2		// disable surfaces popup
		return gizmoName
	endif

	ModifyControlList/Z "pathToSurfaceData", win=GizmoContours, disable=2	// shown disabled because they are readonly

	String/G $GizmoDFVar(gizmoName, "surfaceName") = surfaceName
	
	// show path to the surface data wave, too
	String/G $PanelDFVar("pathToSurfaceData")= pathToSurfaceData // displayed in SetVariable pathToSurfaceData

	// contours popup list of given surface
	String contourList= GetGizmoSurfaceContours(gizmoName, surfaceName)+"_new_;"
	
	String contourName= StrVarOrDefault(GizmoSurfaceDFVar(gizmoName,surfaceName,"contourName"), StringFromList(0,contourList))
	// the user could have removed a contour
	mode= 1 + WhichListItem(contourName, contourList) 
	if( mode < 1 )
		contourName="_new_"
		mode= 1 + WhichListItem(contourName, contourList) 
	endif
	String/G $GizmoSurfaceDFVar(gizmoName,surfaceName,"contourName") = contourName

	PopupMenu contoursOfSurface, win=GizmoContours, mode=mode, popvalue=contourName, title="Contours of "+surfaceName
	String cmd= GetIndependentModuleName()+"#GizmoContours#GizmoSurfaceContourList(\"" + gizmoName + "\", \"" +surfaceName + "\")"
	PopupMenu contoursOfSurface,win=GizmoContours,value=#cmd
	
	if( CmpStr(contourName,"_new_") == 0 )
		ModifyControl removeContour, win=GizmoContours, disable=2	                                                              // show disabled
		CheckBox updateOnChange, win=GizmoContours, disable=1	// Hide the Update immediately checkbox
		Button createOrUpdateContour,win=GizmoContours,title="Append Contours"
		contourName= "Nascent"	// place to put control values when the contour doesn't exist, yet.
	else
		CheckBox updateOnChange, win=GizmoContours, disable=0	// Show the Update immediately checkbox
		Button createOrUpdateContour,win=GizmoContours,title="Update Contours"
	endif
	
	// Restore settings for the selected contour of the selected surface from the data folder (or defaults) to the controls
	
	// Auto Levels
	Variable useAutoLevels= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"useAutoLevels"), 1)	// defaults should be constants
	Variable/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"useAutoLevels")= useAutoLevels
	Checkbox radioAutoLevels, win= GizmoContours, variable= $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"useAutoLevels")
	// proc sets/unsets Checkbox radioListOfLevels
	
	Variable numAutoLevels= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"numAutoLevels"), 10)	// defaults should be constants
	Variable/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"numAutoLevels")= numAutoLevels
	SetVariable numAutoLevels, win= GizmoContours, value=$GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"numAutoLevels")
	
	// List of Levels
	// use list of levels = !useAutoLevels
	Checkbox radioListOfLevels, win= GizmoContours, value= !useAutoLevels
	// proc sets/unsets Checkbox radioAutoLevels and the global
	
	String listOfLevels= StrVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"listOfLevels"), "")	// defaults should be constants
	String/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"listOfLevels")= listOfLevels
	SetVariable listOfLevels, win= GizmoContours, value=$GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"listOfLevels")
	
	// check the levels values
	String errorString= LevelsListErrorString(listOfLevels)
	if( strlen(errorString) )
		TitleBox levelsListError,win= GizmoContours,title=errorString
	else
		TitleBox levelsListError,win= GizmoContours,variable=$GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"listOfLevels")
	endif
	
	// Display at Z=
	
	// values of displayContoursWhere are the corresponding radio button name, one of: "contoursOnSurface;contoursAtMinZ;contoursAtMaxZ;contoursAtSpecificZ"
	String displayContoursWhere= StrVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"displayContoursWhere"), "contoursAtMinZ")	// defaults should be constants
	String/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"displayContoursWhere")= displayContoursWhere

	SetRadioGroup(displayContoursWhere, ksContourAtRadios)

	Variable displayContourZLevel= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"displayContourZLevel"), 0)	// defaults should be constants
	Variable/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"displayContourZLevel")= displayContourZLevel
	SetVariable displayContourZLevel, win= GizmoContours, value=$GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"displayContourZLevel")
	
	// Ribbon Height (% z range) is only for contours on surface
	Variable doRibbon= CmpStr(displayContoursWhere, "contoursOnSurface") == 0
	Variable disable= doRibbon ? 0 : 1
	Variable ribbonHeight= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"ribbonHeight"), 2)	// defaults should be constants
	Variable/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"ribbonHeight")= ribbonHeight
	SetVariable ribbonHeight, win= GizmoContours, value=$GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"ribbonHeight"), disable=disable

	// Calc Normals
	Variable calcNormals= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"calcNormals"), 0)	// defaults should be constants
	Variable/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"calcNormals")= calcNormals
	CheckBox calcNormals, win= GizmoContours, variable=$GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"calcNormals"), disable=disable

	// Path Line Size (points) is only for XY plane contours
	disable= doRibbon ? 1 : 0
	Variable pathLineSize= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"pathLineSize"), 1)	// defaults should be constants
	Variable/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"pathLineSize")= pathLineSize
	SetVariable pathLineSize, win= GizmoContours, value=$GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"pathLineSize"), disable=disable

	// Color Plane stuff is only for XY plane contours
	
	Variable displayColorPlane= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"displayColorPlane"), 0)	// defaults should be constants
	Variable/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"displayColorPlane")= displayColorPlane
	CheckBox displayColorPlane, win= GizmoContours, variable=$GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"displayColorPlane"), disable=disable

	// Hide the other color plane stuff if it isn't selected
	disable= doRibbon || !displayColorPlane

	// Plane color
	Variable planeRed= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"planeRed"), 49151)	// defaults should be constants
	Variable/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"planeRed")= planeRed

	Variable planeGreen= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"planeGreen"), 49152)	// defaults should be constants
	Variable/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"planeGreen")= planeGreen

	Variable planeBlue= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"planeBlue"), 65535)	// defaults should be constants
	Variable/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"planeBlue")= planeBlue

	PopupMenu colorPlaneColor,win= GizmoContours,popColor= (planeRed,planeGreen,planeBlue),value= #"\"*COLORPOP*\"", disable=disable

	// Plane Below/Above
	String planeWhere= StrVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"planeWhere"), "radioPlaneBelowContours")	// defaults should be constants
	String/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"planeWhere")= planeWhere

	CheckBox radioPlaneBelowContours, win= GizmoContours, value= CmpStr(planeWhere, "radioPlaneBelowContours") == 0, disable=disable
	CheckBox radioPlaneAboveContours, win= GizmoContours, value= CmpStr(planeWhere, "radioPlaneAboveContours") == 0, disable=disable

	// Plane Z Offset (% z Range)
	Variable planeZOffsetPct= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"planeZOffsetPct"), 1)	// defaults should be constants
	Variable/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"planeZOffsetPct")= planeZOffsetPct
	SetVariable planeZOffsetPct, win= GizmoContours, value=$GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"planeZOffsetPct"), disable=disable

	// Contour colors
	
	// Which colors
	// values of useWhichColors are the corresponding radio button name, one of: "radioColorFromSurface;radioColorSame;radioColorFromTable;"
	String useWhichColors= StrVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"useWhichColors"), "radioColorFromSurface")	// defaults should be constants
	String/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"useWhichColors")= useWhichColors

	CheckBox radioColorFromSurface, win= GizmoContours, value= CmpStr(useWhichColors, "radioColorFromSurface") == 0
	CheckBox radioColorSame, win= GizmoContours, value= CmpStr(useWhichColors, "radioColorSame") == 0
	CheckBox radioColorFromTable, win= GizmoContours, value= CmpStr(useWhichColors, "radioColorFromTable") == 0

	// Same color
	Variable sameRed= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"sameRed"), 65535)	// defaults should be constants
	Variable/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"sameRed")= sameRed

	Variable sameGreen= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"sameGreen"), 0)	// defaults should be constants
	Variable/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"sameGreen")= sameGreen

	Variable sameBlue= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"sameBlue"), 0)	// defaults should be constants
	Variable/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"sameBlue")= sameBlue

	PopupMenu contourSameColorPop,win= GizmoContours,popColor= (sameRed,sameGreen,sameBlue),value= #"\"*COLORPOP*\""

	// Color table
	String colorTableName=StrVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"colorTableName"), "Rainbow")	// defaults should be constants
	String/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"colorTableName")= colorTableName
	mode= 1+WhichListItem(colorTableName, CTabList())
	PopupMenu contourColorPop,win= GizmoContours,mode=mode

	// Reverse
	Variable reverseColorTable= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"reverseColorTable"), 0)	// defaults should be constants
	Variable/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"reverseColorTable")= reverseColorTable
	CheckBox reverse, win= GizmoContours, variable=$GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"reverseColorTable")

	// First color
	Variable firstColorAtMinZ= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"firstColorAtMinZ"), 1)	// defaults should be constants
	Variable/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"firstColorAtMinZ")= firstColorAtMinZ
	Checkbox firstColorSurfaceMinZ, win= GizmoContours, variable= $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"firstColorAtMinZ")
	// proc sets/unsets Checkbox firstColorAt
	// use First Color at Level = !firstColorAtMinZ
	CheckBox firstColorAt, win= GizmoContours, value= !firstColorAtMinZ
	// proc sets/unsets Checkbox firstColorSurfaceMinZ and the firstColorAtMinZ global
	
	Variable firstColorAtLevel= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"firstColorAtLevel"), 0)	// defaults should be constants
	Variable/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"firstColorAtLevel")= firstColorAtLevel
	SetVariable firstColorZ, win= GizmoContours, value=$GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"firstColorAtLevel")
	
	// Last Color
	Variable lastColorAtMaxZ= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"lastColorAtMaxZ"), 1)	// defaults should be constants
	Variable/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"lastColorAtMaxZ")= lastColorAtMaxZ

	CheckBox lastColorSurfaceMaxZ, win= GizmoContours, variable= $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"lastColorAtMaxZ")
	// proc sets/unsets Checkbox lastColorAt
	// use last Color at Level = !lastColorAtMaxZ
	CheckBox lastColorAt, win= GizmoContours, value= !lastColorAtMaxZ
	// proc sets/unsets Checkbox lastColorSurfaceMaxZ and the lastColorAtMaxZ global
	
	Variable lastColorAtLevel= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"lastColorAtLevel"), 0)	// defaults should be constants
	Variable/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"lastColorAtLevel")= lastColorAtLevel
	SetVariable lastColorZ, win= GizmoContours, value=$GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"lastColorAtLevel")

	// Add or update button enabling
	disable= DisableForCreateOrUpdateContour(gizmoName,surfaceName,contourName)
	ModifyControl createOrUpdateContour, win= GizmoContours, disable=disable

	return gizmoName
End

// list from gizmo includes ALL contours in the Gizmo window (based only on object names)
Static Function/S GizmoContoursList(gizmoName)
	String gizmoName

	// Contours are Gizmo ribbon or path objects named "contour000Lev000", "contour001Lev003", etc.
	// These names are all in the gizmo's top-level object list,
	// even though the contour data folders are all subfolders of a <surfaceName> data folder.
	String contours= ""
	String objects= GizmoList(gizmoName,"objectNameList")

	Variable i=0, nameLen= strlen("contourNN")
	do
		String name= StringFromList(i,objects)
		if( strlen(name) == 0 )
			break
		endif
		// strip name down to just contourNN (remove LevMMM suffix)
		name= name[0,nameLen-1]
		if( stringmatch(name, "contour*") )
			if( FindListItem(name, contours) < 0 )	// avoid duplicates
				contours += name + ";"
			endif
		endif
		i+=1
	while(1)

	return contours
End

// get contours related to the named surface, based on data folders, not Gizmo contour<n> objects.
static Function/S GetGizmoSurfaceContours(gizmoName, surfaceName)
	String gizmoName, surfaceName
	
	String contoursNameList=""
	
	// contours are Gizmo group objects named "contour0", "contour1", etc.
	
	// there are corresponding data folders in root:Packages:GizmoContours:<gizmoName>:Contours for each contour in the Gizmo
	// Each contour data folder (owned) contains the owning surfaceName in String/G surfaceName
	// Each corresponding (owning) surface data folder contains the owned contourName in String/G contourName
	
	String surfaceDF= GizmoSurfaceDF(gizmoName, surfaceName)	// "root:Packages:GizmoContours:<gizmoName>:Surfaces:<surfaceName>", no trailing ":"
	
	Variable index = 0
	do
		String dfName = GetIndexedObjName(surfaceDF, 4, index)
		if (strlen(dfName) == 0)
			break
		endif
		strswitch(dfName)
			case "_none_":
			case "_new_":
			case "Nascent":
				break
			default:
				contoursNameList += dfName+";"
				break
		endswitch
		index += 1
	while(1)

	return SortList(contoursNameList,";",16)
End

// for the Contours of SurfaceN list.
Static Function/S GizmoSurfaceContourList(gizmoName,surfaceName)

	String gizmoName,surfaceName
	return GetGizmoSurfaceContours(gizmoName, surfaceName) +"_new_;"
End

Static Function/S GetGizmoMatrixSurfaceList(gizmoName)	// for Matrices in gizmo selected by Gizmo Contours panel
	String gizmoName	// or "" for top gizmo
	
	String surfaceNameList, surfaceDataPathList
	if( !GetGizmoSurfacesNotPlanes(gizmoName, 1, surfaceNameList, surfaceDataPathList) )
		surfaceNameList="_none_"
	endif
	return surfaceNameList
End

// Returns the number of surfaces in the named or top Gizmo window,
// and lists containing the Gizmo surface names and paths to the surface data.
// The two output lists aren't sorted. This is to preserve the correspondence between surfaceName and data wave.
// Contour Plane surfaces are removed if ignoreContourPlanes is set.
Static Function GetGizmoSurfacesNotPlanes(gizmoName, ignoreContourPlanes, surfaceNameList, surfaceDataPathList)
	String gizmoName
	Variable ignoreContourPlanes
	String &surfaceNameList, &surfaceDataPathList

	Variable numSurfaces= GetGizmoSurfaces(gizmoName, surfaceNameList, surfaceDataPathList,want2DMatrices=1,allow2DFlat=0,want3DParametrics=0,ignoreSubgroups=1)	// only top-level surfaces, not those in a group

	if( numSurfaces > 0 && ignoreContourPlanes )
		String cleanedSurfaceNameList="", cleanedSurfaceDataPathList=""
		Variable index=0
		do
			String surfaceName= StringFromList(index, surfaceNameList)
			String pathToData= StringFromList(index, surfaceDataPathList)	// possibly quoted path
			Variable ignoreThisSurface= PathPointsIntoContourPackage(pathToData)
			if( !ignoreThisSurface )
				cleanedSurfaceNameList += surfaceName+";"
				cleanedSurfaceDataPathList += pathToData+";"	
			endif
			index += 1
		while( index < numSurfaces )
		surfaceNameList= cleanedSurfaceNameList
		surfaceDataPathList= cleanedSurfaceDataPathList
		numSurfaces= ItemsInList(surfaceNameList)
	endif
	
	return numSurfaces
End

Static Function PathPointsIntoContourPackage(path)
	String path
	
	String df=PanelDF()
	Variable matchStart= strsearch(path, df, 0, 2)	// ignore case
	return matchStart == 0	
End

static StrConstant ksPackagePath= "root:Packages:GizmoContours"
static StrConstant ksPackageName = "GizmoContours"

Static Function/S PanelDF()
	NewDataFolder/O root:Packages
	NewDataFolder/O $ksPackagePath
	return ksPackagePath
End

Static Function/S PanelDFVar(varName)
	String varName
	
	return PanelDF()+":"+PossiblyQuoteName(varName)
End

// Set the data folder to a place where Execute can dump all kinds of variables and waves.
// Returns the old data folder.
Static Function/S SetPanelDF()

	String oldDF= GetDataFolder(1)
	NewDataFolder/O/S $PanelDF()	// DF is left pointing here to an existing or created data folder.
	return oldDF
End

// returns the path to the Gizmo-specific per-Gizmo contours package data folder, creating it if needed.
static Function/S GizmoDF(gizmoName)
	String gizmoName
	
	ValidGizmoName(gizmoName)	// if "", set gizmoName to top Gizmo's name
	String df= PackagePerGizmoDFVar(gizmoName,ksPackageName,"")	// "" if no gizmo by that name, else "root:Packages:GizmoContours:PerGizmoData:dfName"
	if( strlen(df) == 0 )
		df= PanelDF()+":Defaults"	// for controls before a gizmo window has been created (or after they've all gone away)
	endif
	NewDataFolder/O $df
	return df
End

// Returns the path to gizmo-specific variable, string or wave, which may not exist.
static Function/S GizmoDFVar(gizmoName,varName)
	String gizmoName,varName
	
	return GizmoDF(gizmoName)+":"+PossiblyQuoteName(varName)
End

// Each Gizmo may have multiple surfaces.
// Returns the path to the suface-specific data folder, creating it if necessary
static Function/S GizmoSurfaceDF(gizmoName, surfaceName)
	String gizmoName, surfaceName
	
	String df= GizmoDF(gizmoName) + ":" + surfaceName
	NewDataFolder/O $df
	return df
End

// Returns the path to surface-specific variable, string or wave, which may not exist.
static Function/S GizmoSurfaceDFVar(gizmoName,surfaceName,varName)
	String gizmoName,surfaceName,varName
	
	return GizmoSurfaceDF(gizmoName,surfaceName)+":"+PossiblyQuoteName(varName)
End

// Each surface may have multiple contours.
// Each surface's contour is documented in a subfolder of the surface data folder,
// even though their corresponding group objects are commingled in the Gizmo's data structures.
// Returns the path to the contour-specific data folder, creating it if necessary.
static Function/S GizmoSurfaceContourDF(gizmoName, surfaceName, contourName)
	String gizmoName, surfaceName, contourName
	
	String df= GizmoSurfaceDF(gizmoName, surfaceName) + ":"+contourName
	NewDataFolder/O $df
	return df
End

// Returns the path to contour-specific variable, string or wave, which may not exist.
static Function/S GizmoSurfaceContourDFVar(gizmoName, surfaceName, contourName,varName)
	String gizmoName, surfaceName, contourName,varName
	
	return GizmoSurfaceContourDF(gizmoName, surfaceName, contourName)+":"+PossiblyQuoteName(varName)
End


Static Function MatrixSurfacesPopMenuProc(ctrlName,popNum,nameOfSurfaceObject) : PopupMenuControl
	String ctrlName
	Variable popNum
	String nameOfSurfaceObject	// name of surface object, should  be one of surfaceNameList
	
	String gizmoName=""	// or "" for top gizmo, or get it from String/G $PanelDFVar("topGizmo")= gizmoName	// for the topGizmoSV SetVariable

	String/G $GizmoDFVar(gizmoName, "surfaceName") = nameOfSurfaceObject

	UpdateGizmoContourPanel()
End

Static Function ContoursPopMenuProc(ctrlName,popNum,popStr) : PopupMenuControl
	String ctrlName
	Variable popNum
	String popStr

	String gizmoName,surfaceName,contourName
	GetPanelGizmoSurfaceAndContour(gizmoName,surfaceName,contourName)

	String/G $GizmoSurfaceDFVar(gizmoName,surfaceName,"contourName") = popStr

	UpdateGizmoContourPanel()
End

// Panel hook
Static Function WindowHook(s)
	STRUCT WMWinHookStruct &s

	Variable rval= 0
	strswitch(s.eventName)
		case "activate":
			UpdateGizmoContourPanel()
			break
	EndSwitch

	return rval
End


// returns number of parameters successfully retrieved: 1, 2, or 3 or on error 0 is returned.
Static Function GetPanelGizmoSurfaceAndContour(gizmoName,surfaceName,contourName)
	String &gizmoName,&surfaceName,&contourName

	Variable numGot= 0
	
	gizmoName= ""
	surfaceName= ""
	contourName= ""
	
	SVAR gn= $PanelDFVar("topGizmo")
	
	if( SVAR_Exists(gn) && strlen(gn) )
		gizmoName= gn
		numGot += 1
		
		SVAR/Z sn= $GizmoDFVar(gizmoName, "surfaceName")
		if( SVAR_Exists(sn) && strlen(sn) )
			surfaceName= sn
			numGot += 1
		
			SVAR/Z cn= $GizmoSurfaceDFVar(gizmoName,surfaceName,"contourName")
			if( SVAR_Exists(cn) && strlen(cn) )
				if( CmpStr(cn,"_new_") == 0 )
					cn= "Nascent"	// because contourName really refers to a not-yet-created contour whose values are in the "Nascent" data folder
				endif
				contourName= cn
				numGot += 1
			endif
		endif		
	endif
	return numGot
End

Static Function GizmoInfoButtonProc(ctrlName) : ButtonControl
	String ctrlName

	String oldDF= SetPanelDF()
	Execute "ModifyGizmo showInfo"
	SetDataFolder oldDF
End


Static Function ContourLevelsRadioProc(ctrlName,checked) : CheckBoxControl
	String ctrlName
	Variable checked

	Variable useAutoLevels= CmpStr(ctrlName,"radioAutoLevels") == 0
	CheckBox radioAutoLevels, win= GizmoContours, value= useAutoLevels
	CheckBox radioListOfLevels, win= GizmoContours, value= !useAutoLevels
	
	String gizmoName,surfaceName,contourName
	GetPanelGizmoSurfaceAndContour(gizmoName,surfaceName,contourName)

	Variable/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"useAutoLevels")= useAutoLevels
	
	ContourNeedsUpdate(1)
End

Static Function NumAutoLevelsSetVarProc(ctrlName,varNum,varStr,varName) : SetVariableControl
	String ctrlName
	Variable varNum
	String varStr
	String varName
	
	String gizmoName,surfaceName,contourName
	GetPanelGizmoSurfaceAndContour(gizmoName,surfaceName,contourName)

	Variable useAutoLevels= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"useAutoLevels"), 1)	// defaults should be constants
	if( useAutoLevels )
		ContourNeedsUpdate(1)
	endif
End

Static Function SetLevelsListToAutoLevelsButton(ctrlName) : ButtonControl
	String ctrlName
	
	String gizmoName,surfaceName,contourName
	GetPanelGizmoSurfaceAndContour(gizmoName,surfaceName,contourName)

	String path=GetSurfaceDataPath(gizmoName,surfaceName)
	WAVE/Z wSurfaceData= $path
	if( !WaveExists(wSurfaceData) )
		DoAlert 0, "Couldn't find surface data wave (to compute auto levels from): "+path
		return 0
	endif

	WaveStats/Q/M=1 wSurfaceData
	Variable zMin= V_min, zMax= V_max
	Variable level
	String listOfLevels=""

	Variable requestedAutoLevels= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"numAutoLevels"), 10)	// defaults should be constants
	
	if(requestedAutoLevels < 2 )
		listOfLevels= num2str(NiceNumber((zMin+zMax)/2))
	else
		Variable firstLevel,levelDelta,numDecimalPlaces	// outputs
		Variable actualLevels = CalcAutoLevels(zMin, zMax,requestedAutoLevels,firstLevel,levelDelta,numDecimalPlaces)
		
		// enumerate the auto levels
		Variable i
		for(i=0; i < actualLevels; i+=1)
			level= firstLevel + i * levelDelta
			String levelStr
			if( numDecimalPlaces )
				sprintf levelStr, "%.*f", numDecimalPlaces, level
			else
				levelStr= num2str(level)
			endif
			if( i != 0 )
				listOfLevels += ", "
			endif
			listOfLevels += levelStr
		endfor
	endif
	
	String/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"listOfLevels") = listOfLevels
	
	// Set useAutoLevels=0 and update the radio buttons.
	Variable/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"useAutoLevels")= 0

	UpdateGizmoContourPanel()

	// call the control proc to update the listOfLevels error
	Variable updateWasDone= LevelsListSetVarProc("",0,listOfLevels,"listOfLevels") // calls ContourNeedsUpdate(1), returns 1 if auto-update was done.

End


// round to 1, 2, or 5 * 10eN, non-rigorously
static Function NiceNumber(num)
	Variable num
	
	if( num == 0 )
		return 0
	endif
	Variable theSign= sign(num)
	num= abs(num)
	Variable lg= log(num)
	Variable decade= floor(lg)
	Variable frac = lg - decade
	Variable mant
	if( frac < log(1.5) )	// above 1.5, choose 2
		mant= 1
	else
		if( frac < log(4) )	// above 4, choose 5
			mant= 2
		else
			if( frac < log(8) )	// above 8, choose 10
				mant= 5
			else
				mant= 10
			endif
		endif
	endif
	num= theSign * mant * 10^decade
	return num
End


// Returns "best" number of levels, first level and delta level
// given: requestedAutoLevels, minLevel, and maxLevel.
//

Static Function CalcAutoLevels(minLevel, maxLevel,requestedAutoLevels,firstLevelP,levelDeltaP,numDecimalPlacesP)
	Variable minLevel			// input, example: min= -13
	Variable maxLevel			// input, example: max= +31.4
	Variable requestedAutoLevels	// input, expected to greater than zero, example = 9
	Variable &firstLevelP		// output
	Variable &levelDeltaP		// output
	Variable &numDecimalPlacesP		// output, digits past dp only if log(levelDelta)<0 (ie, levelDelta < 1)  Also max of 15 digits

	Variable logLevelDelta
	Variable singular= 0
	Variable logIntegerPart
	Variable numPlaces,actualLevels
	Variable span,logFraction, levelDelta, firstLevel, lastLevel
	Variable canonicLevel,levelsFromCanonic
	
	actualLevels= requestedAutoLevels+1	// compensate for the tendency to skip half a level at min and at max
	if( requestedAutoLevels <= 1 || minLevel == maxLevel )
		singular= 1					// calculate only one level
		actualLevels= 1				// prevent division by zero in logLevelDelta calc below
	endif
	span= maxLevel-minLevel
	logLevelDelta= log(abs(span)/actualLevels)	// example: min= -13, max= +31.4 , actualLevels= 10, logLevelDelta= log10(4.44000) = 0.64738
	logIntegerPart= trunc(logLevelDelta)					// example, logIntegerPart= 0
	if( logLevelDelta<0 )
		logIntegerPart -= 1	
	endif					// keeps levelDelta positive
	numPlaces = logIntegerPart						// will become num digits past dp, example = 0
	logFraction= logLevelDelta-logIntegerPart		// logFraction is the fractional part of logLevelDelta
										 			// not expressed by logIntegerPart, and is always between 0 and 1.0. example = 0.64738.
										 			
	// find "nice" increment for levels
	if( logFraction > 0.875 )			// logFraction > log10(7.5)?
		levelDelta=  10
		numPlaces += 1
	elseif( logFraction > 0.544 )		// logFraction > log10(3.5)? example: true, levelDelta= 5
		levelDelta=  5
	elseif( logFraction > 0.176 )		// logFraction > log10(1.5)?
		levelDelta=  2
	else
		levelDelta=  1
	endif
	
	levelDelta= levelDelta*10.0^logIntegerPart	// restore log integer part.  example: levelData= 5*10^0 = 5

	if(span<0)												// can happen if manual max is less than auto/man min, or if manual min is > auto/man max 
		levelDelta=  -levelDelta							// restore sign
	endif
	
	//
	// digits past dp only if log(levelDelta)<0 (ie, levelDelta < 1)  Also max of 15 digits
	
	numPlaces =  min(15,-min(0,numPlaces))		// example = 0

	//
	// actualLevels based on maxLevel, minLevel, and levelDelta,
	// based on CALC_NICE_ACTION code in axis.c
	// Notice that the goal here is different than for an X or Y
	// axis: putting a level below or at the minimum
	// is absolutely useless, so the first level is above that.
	// A level above the maximum will never generate a contour, either.
	
	
	// Find a "nice" level between zMin and zMin+levelDelta

	if( singular )
		firstLevel=  (maxLevel+minLevel)/2.0
		actualLevels= 1
		levelDelta= maxLevel - firstLevel
	else			// Find first level such that 0+m*levelDelta <= firstLevel <= 0+(m+1)*levelDelta
		canonicLevel= 0.0
		levelsFromCanonic= ceil((minLevel-canonicLevel)/levelDelta)	// example, levelsFromCanonic= ceil((-13)/5) = -2
		firstLevel= canonicLevel + levelsFromCanonic*levelDelta		//          firstLevel= 0 + (-2) * 5 = -10
		if( firstLevel == minLevel )									// disallow a level exactly at the min: it doesn't usually produce a contour
			firstLevel += levelDelta
			levelsFromCanonic+=1
		endif
		actualLevels= floor((maxLevel-firstLevel)/levelDelta)  + 1	// number of levelDeltas including firstLevel to lastLevel
																	 // example, floor((31.4-(-10))/5) + 1 = 9.
		lastLevel= firstLevel + (actualLevels-1) * levelDelta
		if( lastLevel == maxLevel && actualLevels > 1 )				// disallow a level exactly at the max: it doesn't usually produce a contour
			actualLevels-=1
		endif
	endif

	levelDeltaP= levelDelta
	firstLevelP= firstLevel
	numDecimalPlacesP= numPlaces
	return requestedAutoLevels <= 1 ? requestedAutoLevels : actualLevels
End



// return text to display in TitleBox levelsListError
Static Function/S LevelsListErrorString(listOfLevels)
	String listOfLevels

	String errorString=""
	Variable i=0
	do
		String str= StringFromList(0,listOfLevels,",")	// comma separated list as accepted by ModifyContour
		String cleanedStr= ReplaceString(" ", str, "")	// remove all spaces
		if( strlen(cleanedStr) == 0 )
			if( ItemsInList(listOfLevels) > 0 )
				errorString= "\\K(65535,0,0) Error at \"" + listOfLevels+"\""
			endif
			break
		endif
		// str2num is way too forgiving: it accepts 2700frk as 2700
		Variable num= str2num(cleanedStr)
		if( numtype(num) != 0 )
			errorString= "\\K(65535,0,0) Error at \"" + str +" ...\""
			break
		endif
		// valid number, move on.
		listOfLevels= RemoveListItem(0,listOfLevels,",")
		i += 1
	while(1)
	
	return errorString[0,62]	// TitleBox has a 63 character limit
End

Static Function LevelsListSetVarProc(ctrlName,varNum,varStr,varName) : SetVariableControl
	String ctrlName
	Variable varNum
	String varStr
	String varName

	// call update when it checks for error when parsing varStr as a list of numbers, display a warning in TitleBox levelsError
	String errorString= LevelsListErrorString(varStr)
	if( strlen(errorString) )
		TitleBox levelsListError,win= GizmoContours,title=errorString
	else
		String gizmoName,surfaceName,contourName
		GetPanelGizmoSurfaceAndContour(gizmoName,surfaceName,contourName)

		TitleBox levelsListError,win= GizmoContours,variable=$GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"listOfLevels")
	endif
	
	return ContourNeedsUpdate(1)	// true if auto-update was done.
End

static Function SetRadioGroup(ctrlName, allRadioNamesList)
	String ctrlName // the checked radio button control's name
	String allRadioNamesList

	Variable i, n= ItemsInList(allRadioNamesList)
	for(i=0; i<n; i+=1)
		String control= StringFromList(i, allRadioNamesList)
		Checkbox $control win=GizmoContours, value= CmpStr(ctrlName,control)==0
	endfor
End

static StrConstant ksColorFromRadios="radioColorFromSurface;radioColorSame;radioColorFromTable;"

Static Function ContourColorsRadioProc(ctrlName,checked) : CheckBoxControl
	String ctrlName
	Variable checked

	SetRadioGroup(ctrlName,ksColorFromRadios)
	
	String gizmoName,surfaceName,contourName
	GetPanelGizmoSurfaceAndContour(gizmoName,surfaceName,contourName)

	String/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"useWhichColors")= ctrlName
	
	ContourNeedsUpdate(1)
End

// Call ContourNeedsUpdate(1) when the contours need an update.
// (needsUpdate should always be 1.)
//
// Returns truth that an update was (automatically) done.
Static Function ContourNeedsUpdate(needsUpdate)
	Variable needsUpdate	// always 1

	String gizmoName,surfaceName,contourName
	GetPanelGizmoSurfaceAndContour(gizmoName,surfaceName,contourName)

	Variable/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"contourNeedsUpdate")= 1	// for Update routine

	// if needsUpdate could EVER be 0:
	if( !needsUpdate )
		needsUpdate= ContoursAreStale(gizmoName,surfaceName,contourName)
	endif

	Variable isNew= CmpStr(contourName, "Nascent") == 0

	// see if the user wants auto-updating
	ControlInfo/W=GizmoContours updateOnChange
	Variable updateDone= V_Value && needsUpdate && !isNew
	if( updateDone )
		// pretend the createOrUpdateContour was pressed
		AddOrUpdateButtonProc("")	// calls 	UpdateGizmoContourPanel()
	else
		ModifyControl createOrUpdateContour, win= GizmoContours, disable=needsUpdate ? 0 : 2	// enable this button whenever the user changes anything.
	endif
	return updateDone
End

Static Function ColorTablePopMenuProc(ctrlName,popNum,colorTableName) : PopupMenuControl
	String ctrlName
	Variable popNum
	String colorTableName	// popStr
	
	String gizmoName,surfaceName,contourName
	GetPanelGizmoSurfaceAndContour(gizmoName,surfaceName,contourName)
	
	String/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"colorTableName")= colorTableName

	String/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"useWhichColors")= "radioColorFromTable"	// turn on Use Color Table
	UpdateGizmoContourPanel()
	ContourNeedsUpdate(1)
End

Static Function ReverseCheckProc(ctrlName,checked) : CheckBoxControl
	String ctrlName
	Variable checked
	
	String gizmoName,surfaceName,contourName
	GetPanelGizmoSurfaceAndContour(gizmoName,surfaceName,contourName)
	
	Variable/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"reverseColorTable")= checked

	String/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"useWhichColors")= "radioColorFromTable"	// turn on Use Color Table
	UpdateGizmoContourPanel()
	ContourNeedsUpdate(1)
End

Static Function FirstColorRadioProc(ctrlName,checked) : CheckBoxControl
	String ctrlName
	Variable checked

	Variable firstColorAtMinZ= CmpStr(ctrlName,"firstColorSurfaceMinZ") == 0
	CheckBox firstColorSurfaceMinZ, win= GizmoContours, value= firstColorAtMinZ
	CheckBox firstColorAt, win= GizmoContours, value= !firstColorAtMinZ
	
	String gizmoName,surfaceName,contourName
	GetPanelGizmoSurfaceAndContour(gizmoName,surfaceName,contourName)

	Variable/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"firstColorAtMinZ")= firstColorAtMinZ
	
	String useWhichColors= StrVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"useWhichColors"), "radioColorFromSurface")	// defaults should be constants
	if( CmpStr(useWhichColors, "radioColorFromTable") == 0 )
		ContourNeedsUpdate(1)
	endif
End

Static Function FirstColorAtZSetVarProc(ctrlName,varNum,varStr,varName) : SetVariableControl
	String ctrlName
	Variable varNum
	String varStr
	String varName

	String gizmoName,surfaceName,contourName
	GetPanelGizmoSurfaceAndContour(gizmoName,surfaceName,contourName)

	String useWhichColors= StrVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"useWhichColors"), "radioColorFromSurface")	// defaults should be constants
	if( CmpStr(useWhichColors, "radioColorFromTable") == 0 )
		Variable firstColorAtMinZ= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"firstColorAtMinZ"), 1)	// defaults should be constants
		if( !firstColorAtMinZ )
			ContourNeedsUpdate(1)
		endif
	endif
End


Static Function LastColorRadioProc(ctrlName,checked) : CheckBoxControl
	String ctrlName
	Variable checked

	Variable lastColorAtMaxZ= CmpStr(ctrlName,"lastColorSurfaceMaxZ") == 0
	CheckBox lastColorSurfaceMaxZ, win= GizmoContours, value= lastColorAtMaxZ
	CheckBox lastColorAt, win= GizmoContours, value= !lastColorAtMaxZ
	
	String gizmoName,surfaceName,contourName
	GetPanelGizmoSurfaceAndContour(gizmoName,surfaceName,contourName)

	Variable/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"lastColorAtMaxZ")= lastColorAtMaxZ
	
	String useWhichColors= StrVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"useWhichColors"), "radioColorFromSurface")	// defaults should be constants
	if( CmpStr(useWhichColors, "radioColorFromTable") == 0 )
		ContourNeedsUpdate(1)
	endif
End

Static Function LastColorAtZSetVarProc(ctrlName,varNum,varStr,varName) : SetVariableControl
	String ctrlName
	Variable varNum
	String varStr
	String varName

	String gizmoName,surfaceName,contourName
	GetPanelGizmoSurfaceAndContour(gizmoName,surfaceName,contourName)

	String useWhichColors= StrVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"useWhichColors"), "radioColorFromSurface")	// defaults should be constants
	if( CmpStr(useWhichColors, "radioColorFromTable") == 0 )
		Variable lastColorAtMaxZ= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"lastColorAtMaxZ"), 1)	// defaults should be constants
		if( !lastColorAtMaxZ )
			ContourNeedsUpdate(1)
		endif
	endif
End

static StrConstant ksContourAtRadios= "contoursOnSurface;contoursAtMinZ;contoursAtMaxZ;contoursAtSpecificZ;"

Static Function DisplayContoursRadioProc(displayContoursWhere,checked) : CheckBoxControl
	String displayContoursWhere	// ctrlName
	Variable checked

	SetRadioGroup(displayContoursWhere, ksContourAtRadios)
	
	String gizmoName,surfaceName,contourName
	GetPanelGizmoSurfaceAndContour(gizmoName,surfaceName,contourName)

	String/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"displayContoursWhere")= displayContoursWhere
	
	ContourNeedsUpdate(1)
	UpdateGizmoContourPanel()	// show hide the ribbon or path height and calc normals
End

Static Function DisplayContoursAtZSetVarProc(ctrlName,varNum,varStr,varName) : SetVariableControl
	String ctrlName
	Variable varNum
	String varStr
	String varName

	String gizmoName,surfaceName,contourName
	GetPanelGizmoSurfaceAndContour(gizmoName,surfaceName,contourName)

	String displayContoursWhere= StrVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"displayContoursWhere"), "contoursOnSurface")	// defaults should be constants
	if( CmpStr(displayContoursWhere, "contoursAtSpecificZ") == 0 )
		ContourNeedsUpdate(1)
	endif
End

Static Function RemoveContourButtonProc(ctrlName) : ButtonControl
	String ctrlName

	String gizmoName,surfaceName,contourName
	GetPanelGizmoSurfaceAndContour(gizmoName,surfaceName,contourName)
	
	strswitch(contourName)
		case "_new_":
		case "Nascent":
			Beep
			break
		default:
			String contoursBefore= GizmoSurfaceContourList(gizmoName,surfaceName)
			RemoveGizmoContour(gizmoName, surfaceName, contourName)
			RecompileGizmo(gizmoName)
			
			Variable whichOne= WhichListItem(contourName,contoursBefore)
			if( whichOne == 0 )
				whichOne= 1
			else
				whichOne -= 1
			endif
			contourName= StringFromList(whichOne, contoursBefore)
			String/G $GizmoSurfaceDFVar(gizmoName,surfaceName,"contourName") = contourName
			
			// set list to previous contour
			UpdateGizmoContourPanel()
			break
	endswitch

End


Static Function RemoveGizmoContour(gizmoName, surfaceName, contourName)
	String gizmoName, surfaceName, contourName
	
	RemoveMatchingGizmoObjects(gizmoName,contourName+"*")
	RemoveMatchingGizmoDisplay(gizmoName,contourName+"*")
	String contourDF= GizmoSurfaceContourDF(gizmoName,surfaceName,contourName)			
	KillDataFolder/Z contourDF
End


Static Function AddOrUpdateButtonProc(ctrlName) : ButtonControl
	String ctrlName
	
	// checks:
	String gizmoName,surfaceName,contourName
	GetPanelGizmoSurfaceAndContour(gizmoName,surfaceName,contourName)
	
	// if using List Of Levels
	Variable useAutoLevels= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"useAutoLevels"), 1)	// defaults should be constants
	if( !useAutoLevels )
		// if error in List of Levels
		String listOfLevels= StrVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"listOfLevels"), "")	// defaults should be constants
		String errorString= LevelsListErrorString(listOfLevels)
		if( strlen(errorString) )
			DoAlert 0, "Fix error in List of Levels in order to proceed!"
			SetVariable/Z  listOfLevels win= GizmoContours,  activate
			return 0
		endif
	endif
	
	contourName= AddNewOrUpdateExistingContour(gizmoName,surfaceName,contourName)		// contour name can be "Default" to mean "_new_"
	if( strlen(contourName) )
		String/G $GizmoSurfaceDFVar(gizmoName,surfaceName,"contourName") = contourName
		Variable/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"contourNeedsUpdate")= 0
	endif
	UpdateGizmoContourPanel()
End

// returns the existing or new contour name
static Function/S AddNewOrUpdateExistingContour(gizmoName,surfaceName,contourName)
	String gizmoName,surfaceName,contourName

	String df= GizmoSurfaceContourDF(gizmoName,surfaceName,contourName)
	strswitch(contourName)
		case "_new_":
		case "Nascent":
			String srcDF= GizmoSurfaceContourDF(gizmoName,surfaceName,contourName)
			contourName= UniqueContourName(gizmoName)
			if( strlen(contourName) == 0 )
				DoAlert 0, "Could not create new contour: too many already!"
				return ""
			endif
			df= GizmoSurfaceContourDF(gizmoName,surfaceName,contourName)	// creates the data folder
			KillDataFolder/Z $df	// DuplicateDataFolder requires the folder to be absent
			DuplicateDataFolder $srcDF, $df
			break
		default:
			RemoveMatchingGizmoObjects(gizmoName,contourName+"*")	// don't call RemoveGizmoContour, because that kills the entire contour data folder.
			break
	endswitch
	NewContour(gizmoName,surfaceName,contourName)
	return contourName
End


Static Function/S UniqueContourName(gizmoName)
	String gizmoName

	String contours= GizmoContoursList(gizmoName)	// list from gizmo includes ALL contour groups in the Gizmo window
	Variable suffix= 0
	do
		String contourName
		sprintf contourName, "contour%.2d",suffix	// always use 2 digits instead of 1, so that deleting contour1* doesn't delete contour10.
		Variable offset= FindListItem(contourName, contours)
		if( offset < 0 )	// unique?
			return contourName	// yep
		endif
		suffix += 1
	while(suffix <= 99)

	return ""	// error
End


Static Function NewContour(gizmoName,surfaceName,contourName)
	String gizmoName,surfaceName,contourName	// name of the new contour for the given surface.
	
	// generate the contours using a graph posing as a progress dialog.
	
	// Contour the surface data wave
	String path=GetSurfaceDataPath(gizmoName,surfaceName)
	WAVE/Z wSurfaceData= $path
	if( !WaveExists(wSurfaceData) )
		DoAlert 0, "Couldn't find surface data wave: "+path
		return 0
	endif

	String oldDF= SetPanelDF()
	
	WaveStats/Q/M=1 wSurfaceData
	Variable zMin= V_min, zMax= V_max
	
	String wName= NameOfWave(wSurfaceData)
	String msg= "Generating "+contourName+" of "+wName +"..."
	DoWindow/K ComputeGizmoContour
	Display/N=ComputeGizmoContour/W=(20,20,220,60) as msg
	String win= S_Name
	AutoPositionWindow/E/M=1/R=$gizmoName $win
	GetWindow $win gsize
	Variable pixels= (V_bottom - V_top) * ScreenResolution/72
	ControlBar/W=$win pixels	// don't show the contours, show a message
	
	Variable vCenter= max(20,pixels/2)
	TitleBox message win=$win, frame=0, pos={20, vCenter-10}, title="\\K(65535,0,0)"+msg
	// create a contour plot offscreen (hidden in the graph)
	AppendMatrixContour/W=$win wSurfaceData
	
	String graphContourName= NameOfWave(wSurfaceData)
	
	// modify the contour to match the levels
	String cmdBase="ModifyContour/W="+win+" "+graphContourName+" "
	String cmd, cmdAddition=""
	Variable useAutoLevels= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"useAutoLevels"), 1)	// defaults should be constants
	if( useAutoLevels )
		Variable numAutoLevels= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"numAutoLevels"), 10)	// defaults should be constants
		cmdAddition= "autoLevels={*,*,"+num2istr(numAutoLevels) + "}"
		cmd= cmdBase+ cmdAddition
	else
		cmd= cmdBase+ "autoLevels={*,*,0}"
		Execute cmd
	
		String listOfLevels= StrVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"listOfLevels"), "")	// defaults should be constants
		cmdAddition="moreLevels={"+listOfLevels+"}"
		cmd= cmdBase+ cmdAddition
	endif	
	Execute/Q/Z cmd
	if( V_Flag != 0 )
		DoWindow/K ComputeGizmoContour
		DoAlert 0, "Fix error in contour levels to proceed: "+cmdAddition
		DoWindow GizmoContours
		if( V_Flag )
			SetVariable/Z  listOfLevels win= GizmoContours,  activate
		endif
		SetDataFolder oldDF
		return 0
	endif

	// compute the colors, we don't care what they are in the contour graph, really.
	String colorTableName="Rainbow"	// defaults should be constants
	Variable reverseColorTable=0		// defaults should be constants
	Variable firstColorAtMinZ=1, lastColorAtMaxZ=1	// defaults should be constants
	Variable firstColorAtLevel, lastColorAtLevel= 0			// defaults should be constants

	Variable minRed, minGreen, minBlue, minAlpha	// useColorsFromSurface values
	Variable maxRed, maxGreen, maxBlue, maxAlpha

	String useWhichColors= StrVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"useWhichColors"), "radioColorFromSurface")	// defaults should be constants
	Variable useColorsFromSurface= 0
	strswitch( useWhichColors )
		case "radioColorFromSurface":
			Variable surfaceColorType= GetGizmoSurfaceColors(gizmoName, surfaceName, colorTableName, reverseColorTable, firstColorAtLevel, minRed, minGreen, minBlue, minAlpha, lastColorAtLevel, maxRed, maxGreen, maxBlue, maxAlpha)
			Variable validColors= surfaceColorType == 1 || surfaceColorType == 2
			if( !validColors )
				DoAlert 0, "Constant or table-based surface colors required for Use Colors from Surface. Switching to Use Same Color for "+surfaceName+"."; Beep
			else
				useColorsFromSurface= 1
				break
			endif
			useWhichColors= "radioColorSame"
			String/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"useWhichColors")=useWhichColors
			SetRadioGroup(useWhichColors,ksColorFromRadios)
			// fall through
		case "radioColorSame":
			// ModifyContour 'HALF DOME' rgbLines=(65535,0,0)
			Variable sameRed= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"sameRed"), 65535)	// defaults should be constants
			Variable sameGreen= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"sameGreen"), 0)	// defaults should be constants
			Variable sameBlue= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"sameBlue"), 0)	// defaults should be constants
			sprintf cmd, cmdBase+"rgbLines=(%d, %d, %d)",sameRed, sameGreen, sameBlue
			Execute cmd
			break
		case "radioColorFromTable":
			// color scaling
			firstColorAtMinZ= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"firstColorAtMinZ"), 1)	// defaults should be constants
			if( firstColorAtMinZ )
				firstColorAtLevel= zMin
			else
				firstColorAtLevel= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"firstColorAtLevel"), 0)	// defaults should be constants
			endif
	
			lastColorAtMaxZ= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"lastColorAtMaxZ"), 1)	// defaults should be constants
			if( lastColorAtMaxZ )
				lastColorAtLevel= zMax
			else
				lastColorAtLevel= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"lastColorAtLevel"), 0)	// defaults should be constants
			endif
			
			// color table
			colorTableName=StrVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"colorTableName"), "Rainbow")	// defaults should be constants
			reverseColorTable= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"reverseColorTable"), 0)	// defaults should be constants
	
			// apply these to the contour plot
			sprintf cmd, cmdBase+"ctabLines={%.14g, %.14g, %s, %d}",firstColorAtLevel, lastColorAtLevel, colorTableName, reverseColorTable
			Execute cmd
			break
	endswitch

	DoUpdate	// update contour traces

	// extract contour waves into a subfolder of the contour data folder
	String contourDF= GizmoSurfaceContourDF(gizmoName,surfaceName,contourName)
	SetDataFolder $contourDF
	String extractedTracesDF= contourDF +":extractedContourTraces"
	KillDataFolder/Z $extractedTracesDF	// clean out old extracted traces
	Variable n= WMDuplicateDisplayContourWaves(win, graphContourName, extractedTracesDF,"")
	SetDataFolder oldDF
	if( n < 1 )
		DoAlert 0, "No contours to append"
		KillDataFolder/Z $extractedTracesDF
		return 0
	endif

	// Mark the date and time the contours as being contemporaneous with the last modification of the underlying surface data
	// see ContoursAreStale()
	Variable/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"contourModDate")=modDate(wSurfaceData)	// or = DateTime
	
	// Reassign colors to traces if useColorsFromSurface, see how WMDuplicateDisplayContourWaves puts them in there.
	if( useColorsFromSurface && validColors )
		Variable red, green, blue
		Variable zval
		String rgbstr
		
		SetDataFolder extractedTracesDF
		Variable zRange, multiplier,numColors,index
		
		if( strlen(colorTableName) )
			// create a color index wave based on the color settings
			ColorTab2Wave $colorTableName
			WAVE M_colors	// nx3 of red, green, blue from 0-65535
			//SetScale/I x, firstColorAtLevel, lastColorAtLevel, "", M_Colors
			// keep unscaled. Use the Igor image plot mapping:
			//	index= floor((z-zmin)/(zmax-zmin)*numColors)
			numColors= DimSize(M_colors,0)
			zRange= lastColorAtLevel-firstColorAtLevel
			multiplier= numColors/zRange
		else
			red= minRed
			green= minGreen
			blue= minBlue
		endif
				
		String waves= WaveList("*.y", ";", "DIMS:1,TEXT:0")
		Variable i=0
		do
			String wny= StringFromList(i,waves)
			Wave/Z wy= $wny
			if( !WaveExists(wy) )
				break
			endif
			String wnx= ReplaceString(".y", wny, ".x")
			WAVE/Z wx= $wnx
			if( !WaveExists(wx) )
				break
			endif
			i += 1	

			if( strlen(colorTableName) )
				// use the contour level's z value and gizmo surface colors
				// to compute the color to assign to the trace
				
				// get the z value from wave's name: <contourInstance>=<z>.x
				zval= 0	// in case of error
				Variable equalPos= strsearch(wnx,"=", 0)
				sscanf wnx[equalPos,inf], "=%g.x",  zval
				if( zval < firstColorAtLevel )
					red= minRed*65535
					green= minGreen*65535
					blue= minBlue*65535
				elseif( zval > lastColorAtLevel )
					red= maxRed*65535
					green= maxGreen*65535
					blue= maxBlue*65535
				else
					index= floor((zval-firstColorAtLevel)*multiplier)
					red= M_colors[index][0]
					green= M_colors[index][1]
					blue= M_colors[index][2]
				endif
			endif

			sprintf rgbstr, ";RED:%d;GREEN:%d;BLUE:%d", red, green, blue
			Note/K wy
			Note wy, rgbstr	// replace the note
		while( 1 )
		
		if( strlen(colorTableName) )
			KillWaves/Z M_colors
		endif
		SetDataFolder oldDF
	endif
	
	// create ribbon objects if contoursOnSurface, otherwise create path objects at a constant z level.
	String displayContoursWhere= StrVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"displayContoursWhere"), "contoursOnSurface")	// defaults should be constants
	
	Variable doRibbons= 0
	Variable displayContourZLevel= 0
	
	strswitch(displayContoursWhere)
		case "contoursOnSurface":
			doRibbons= 1
			break
		case "contoursAtMinZ":
			displayContourZLevel= zMin
			break
		case "contoursAtMaxZ":
			displayContourZLevel= zMax
			break
		case "contoursAtSpecificZ":
			displayContourZLevel= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"displayContourZLevel"), 0)	// defaults should be constants
			break
	endswitch
	
	Variable pathLineSize, lineWidthType
	Variable calcNormals=0
	if( doRibbons )
		// Ribbon height is specified as a percentage of the z range
		Variable ribbonHeightPct= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"ribbonHeight"), 2)	// defaults should be constants
		Variable ribbonHeight= (zMax-zMin) * ribbonHeightPct / 100
		calcNormals= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"calcNormals"), 0)	// defaults should be constants
		lineWidthType= 0
		pathLineSize= 1
	else
		// Path Line Size (points)
		lineWidthType= 1
		pathLineSize= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"pathLineSize"), 1)	// defaults should be constants
	endif
	
	String contourWavesDF= contourDF +":contourWaves"
	KillDataFolder/Z contourWavesDF	// clean out old stuff
	NewDataFolder $contourWavesDF	// NewDataFolder requires a name.

	SetPanelDF()
	cmd= "ModifyGizmo/N="+gizmoName+" startRecMacro"	
	Execute cmd
	SetDataFolder oldDF
	
	String objName

	SetDataFolder extractedTracesDF
	waves= WaveList("*.y", ";", "DIMS:1,TEXT:0")
	i=0
	do
		SetDataFolder extractedTracesDF
		wny= StringFromList(i,waves)
		Wave/Z wy= $wny
		if( !WaveExists(wy) )
			break
		endif
		wnx= ReplaceString(".y", wny, ".x")
		WAVE/Z wx= $wnx
		if( !WaveExists(wx) )
			break
		endif
	
		// get color from colortable and z value.
		rgbstr= note(wy)
		red= NumberByKey("RED", rgbstr)/65535
		green= NumberByKey("GREEN", rgbstr)/65535
		blue= NumberByKey("BLUE", rgbstr)/65535
		
		SetDataFolder contourWavesDF	// create the ribbon or path in a separate data folder apart from the soon-to-be-deleted extracted xy waves
		sprintf objname, "%sLev%.3d", contourName, i
		// NOTE: objName becomes different for ribbons and for paths

		if( doRibbons )

			// get the z value from wave's name: <contourInstance>=<z>.x
			zval= 0	// in case of error
			equalPos= strsearch(wnx,"=", 0)
			sscanf wnx[equalPos,inf], "=%g.x",  zval
	
			// create a ribbon
			objname += "r"
			String wnr= ReplaceString(".x", wnx, ".r")
			Variable rows=DimSize(wx,0)
			Make/O/N=(2*rows,3) $wnr	// in the contourWaves data folder inside the Contour<n> data folder.
			WAVE ribbon= $wnr
			Variable zLow=zval-ribbonHeight/2
			Variable zHigh=zval+ribbonHeight/2
			ribbon[][0]=wx[trunc(p/2)]
			ribbon[][1]=wy[trunc(p/2)]
			ribbon[][2]=mod(p,2)==0 ? zLow : zHigh
			
			// append the ribbon to the current Gizmo group
			// AppendToGizmo Ribbon=root:Packages:GizmoContours:Gizmo0:surface0:contour0:contourWaves:'HALF DOME=1900.xyz',name=HALF_DOME_1900_r
			sprintf cmd, "AppendToGizmo/N=%s Ribbon=%s, name=%s", gizmoName, GetWavesDataFolder(ribbon,2), objName
			Execute cmd
		else
			// create a path. 
			objname += "p"
			String wnt= ReplaceString(".x", wnx, ".xyz")
			Duplicate/O wx, $wnt
			WAVE triplet= $wnt
			Redimension/N=(-1,3) triplet
			triplet[][1]= wy[p]
			triplet[][2]= displayContourZLevel	// All paths are at the same z level=  displayContourZLevel
			
			// Append the path to the current Gizmo group
			sprintf cmd, "AppendToGizmo/N=%s Path=%s, name=%s", gizmoName, GetWavesDataFolder(triplet,2), objName
			Execute cmd
		endif
		
		sprintf cmdBase, "ModifyGizmo/N=%s ModifyObject=%s, property={ ", gizmoName, objName
		if( calcNormals )
			// ModifyGizmo modifyObject=contour00Lev000r property={calcNormals,1}
			Execute cmdBase+"calcNormals,1}"
		endif	

		// set the color and line size
		
		// ModifyGizmo ModifyObject=HALF_DOME_1900_xyz property={ pathColorType,1}
		Execute cmdBase+"pathColorType,1}"

		// ModifyGizmo ModifyObject=HALF_DOME_1900_xyz property={ lineWidthType,0}	  0 for ribbons, 1 for paths
		sprintf cmd, "%slineWidthType,%g}", cmdBase, lineWidthType
		Execute cmd

		// ModifyGizmo ModifyObject=HALF_DOME_1900_xyz property={ lineWidth,1}
		sprintf cmd, "%slineWidth,%g}", cmdBase, pathLineSize
		Execute cmd

		// ModifyGizmo ModifyObject=HALF_DOME_1900_xyz property={ pathColor,r,g,b,1}
		sprintf cmd, "%spathColor,%g,%g,%g,1}", cmdBase, red, green, blue
		Execute cmd

		// Display the Gizmo path object.
		sprintf cmd, "ModifyGizmo/N=%s setDisplayList=-1, object=%s", gizmoName, objName
		Execute cmd

		i += 1	
	while( i <= 999 )

	// df is extractedTracesDF
	
	// color plane
	Variable displayColorPlane= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"displayColorPlane"), 0)	// defaults should be constants
	if( !doRibbons && displayColorPlane )
		objName= contourName +"Plane"
	
		// Plane color
		red= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"planeRed"), 49151)/65535	// defaults should be constants
		green= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"planeGreen"), 49152)/65535	// defaults should be constants
		blue= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"planeBlue"), 65535)/65535	// defaults should be constants

		// Plane Below/Above
		String planeWhere= StrVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"planeWhere"), "radioPlaneBelowContours")	// defaults should be constants

		// Plane Z Offset (% z Range)
		Variable planeZOffsetPct= NumVarOrDefault(GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"planeZOffsetPct"), 1)	// defaults should be constants
		Variable planeOffsetZ= (zMax-zMin) * planeZOffsetPct / 100
		if( CmpStr(planeWhere, "radioPlaneBelowContours") == 0 )
			planeOffsetZ= -planeOffsetZ
		endif
		Variable planeZ= displayContourZLevel + planeOffsetZ

		// make the smallest surface Gizmo can display = planeZ
		String pathToPlaneSurface= GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,objName)
		Make/O/N=(4,4) $pathToPlaneSurface = planeZ
		WAVE plane= $pathToPlaneSurface
		// Get the X and Y coordinates of the plane from wSurfaceData's X and Y Scaling
		CopyScales/I wSurfaceData, plane

		// Append the surface
		SetPanelDF()

		sprintf cmd, "AppendToGizmo/N=%s Surface=%s, name=%s", gizmoName, pathToPlaneSurface, objName
		Execute cmd

		sprintf cmdBase, "ModifyGizmo/N=%s ModifyObject=%s, property={ ", gizmoName, objName
		
		// ModifyGizmo ModifyObject=surface0 property={ surfaceColorType,1}
		Execute cmdBase+"surfaceColorType,1}"

		// ModifyGizmo ModifyObject=surface0 property={ srcMode,0}
		Execute cmdBase+"srcMode,0}"

		// Assign a single color to both front and back
		sprintf cmd, "%sfrontColor,%g,%g,%g,1}", cmdBase, red, green, blue
		Execute cmd
		sprintf cmd, "%sbackColor,%g,%g,%g,1}", cmdBase, red, green, blue
		Execute cmd

		// support shading
		Execute cmdBase+"calcNormals,1}"

		// Display the surface
		sprintf cmd, "ModifyGizmo/N=%s setDisplayList=-1, object=%s", gizmoName, objName
		Execute cmd
	endif
		
	SetPanelDF()
	Execute "ModifyGizmo/N=" + gizmoName + " compile"
	Execute "ModifyGizmo/N=" + gizmoName + " endRecMacro"

	// setup the gizmo's hook to kill the contours when it closes
	UpdateHookFunction(gizmoName, "GizmoContours", "WMGizmoContoursNamedHook", "WMGizmoContoursHook")
	
	SetDataFolder oldDF
	KillDataFolder/Z extractedTracesDF	// extracted contour traces

	DoWindow/K $win
	
End

// Returns truth that the contours are older than the underlying surface data
Static Function ContoursAreStale(gizmoName,surfaceName,contourName)
	String gizmoName,surfaceName,contourName
	
	Variable stale= 0	// presume not (this covers old experiments without contourModDate).
	
	String path=GetSurfaceDataPath(gizmoName,surfaceName)
	WAVE/Z wSurfaceData= $path
	if( WaveExists(wSurfaceData) )
		Variable waveMod= modDate(wSurfaceData)
		NVAR/Z contourModDate= $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"contourModDate")
		if( NVAR_Exists(contourModDate) )
			stale = waveMod > contourModDate
		endif
	endif
	return stale
End

// Returns correct disable value for ModifyControl createOrUpdateContour
Static Function DisableForCreateOrUpdateContour(gizmoName,surfaceName,contourName)
	String gizmoName,surfaceName,contourName

	Variable isNew= CmpStr(contourName, "Nascent") == 0
	Variable needUpdate= isNew

	if( !needUpdate )
		needUpdate= ContoursAreStale(gizmoName,surfaceName,contourName)
	endif
	if( !needUpdate )
		NVAR/Z contourNeedsUpdate= $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"contourNeedsUpdate")
		if( NVAR_Exists(contourNeedsUpdate) )
			needUpdate= contourNeedsUpdate
		endif
	endif
	Variable disable= needUpdate ? 0 : 2
#if 0
	// Perhaps always enable if the "Update Immediately" checkbox is unchecked ?
	if( disable != 0 )
		ControlInfo/W=GizmoContours updateOnChange
		if( V_Value == 0 )	// unchecked
			disable= 0		// enable
		endif
	endif
#endif

	return disable
End

	
// new named Gizmo hook
Function WMGizmoContoursNamedHook(s)
	STRUCT WMGizmoHookStruct &s

	strswitch(s.eventName)
		case "kill":
			Execute/P/Q/Z "GizmoContours#PossiblyKillGizmoContours(\"" + s.winName + "\")"	// once the window has gone away
			break
	endswitch
	return 0
End


// Gizmo Hook function needs to be public - old unnamed hook
Function WMGizmoContoursHook(infoStr)
	String infoStr
	
	String gizmoName= StringByKey("WINDOW",infoStr)
	String event= StringByKey("EVENT",infoStr)
	strswitch(event)
		case "kill":
			Execute/P/Q/Z "GizmoContours#PossiblyKillGizmoContours(\"" + gizmoName + "\")"	// once the window has gone away
			break
		default:
			UpdateHookFunction(gizmoName, "GizmoContours", "WMGizmoContoursNamedHook", "WMGizmoContoursHook")
			break
	endswitch

	return 0
End

// gets the un-named hook function
Static Function/S GetGizmoHookFunction(gizmoName)
	String gizmoName
	
	String code= WinRecreation(gizmoName, 0) // 	ModifyGizmo hookFunction=GizmoRotationHook
	String hookFunction=""
	String key="ModifyGizmo hookFunction="
	Variable start= Strsearch(code, key, 0)
	if( start >= 0 )
		start += strlen(key)	// point past what we just found.
		key= num2char(13)
		Variable theEnd= strsearch(code,key, start)
		if( theEnd >= 0 )
			hookFunction= code[start,theEnd-1]
		endif
	endif
	return hookFunction
End

Static Function UpdateHookFunction(gizmoName, hookName, namedHookFunction, unnamedHookFunction)
	String gizmoName, hookName, namedHookFunction, unnamedHookFunction
	
	String oldDF=SetPanelDF()
	String currentHookFunction= GetGizmoHookFunction(gizmoName)
	// Is this my old hook un-named function?
	if( CmpStr(currentHookFunction, unnamedHookFunction) == 0 )
		// yep, disable it, because we're going to use the named hook function
		Execute "ModifyGizmo/N="+gizmoName+" hookFunction=$\"\""
		Execute "ModifyGizmo/N="+gizmoName+" hookEvents=0"
	endif
	// install the named hook function
	if( strlen(namedHookFunction) == 0 )
		namedHookFunction= "$\"\""
	endif
	Execute "ModifyGizmo/N="+gizmoName+" namedHook={"+hookName+","+namedHookFunction+"}"
	SetDataFolder oldDF
End	

Static Function PossiblyKillGizmoContours(gizmoName)
	String gizmoName
	
	String df= PanelDFVar(gizmoName)
	if( strlen(df) )
		if( exists(gizmoName) != 5 )
			// no recreation macro exists, it's okay to kill the data folder.
			KillDataFolder/Z df
			// if this is the last one, kill all the Packages:Gizmo3DPieChart data folder.
			df= PanelDF()
			String dfName = GetIndexedObjName(df, 4, 0)
			if (strlen(dfName) == 0)
				// no more contours!
				DoWindow/K GizmoContours
				KillDataFolder/Z df
			endif
			return 1
		endif
	endif
	DoWindow GizmoContours
	if( V_Flag )
		UpdateGizmoContourPanel()
	endif
	return 0
End

Static Function SameColorPopMenuProc(ctrlName,popNum,popStr) : PopupMenuControl
	String ctrlName
	Variable popNum
	String popStr
	
	String gizmoName,surfaceName,contourName
	GetPanelGizmoSurfaceAndContour(gizmoName,surfaceName,contourName)
	
	ControlInfo/W=GizmoContours $ctrlName
	
	Variable/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"sameRed")= V_red
	Variable/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"sameGreen")= V_green
	Variable/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"sameBlue")= V_blue

	String/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"useWhichColors")= "radioColorSame"
	ContourNeedsUpdate(1)
	UpdateGizmoContourPanel()
End

Static Function SetVarProc(ctrlName,varNum,varStr,varName) : SetVariableControl
	String ctrlName
	Variable varNum
	String varStr
	String varName
	
	ContourNeedsUpdate(1)
End


Static Function HelpButtonProc(ctrlName) : ButtonControl
	String ctrlName

	DisplayHelpTopic/K=1 "Append Contour To Gizmo"
End

Static Function DisplayColorPlaneCheckProc(ctrlName,checked) : CheckBoxControl
	String ctrlName
	Variable checked

	String gizmoName,surfaceName,contourName
	GetPanelGizmoSurfaceAndContour(gizmoName,surfaceName,contourName)
	
	Variable/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"displayColorPlane")= checked

	UpdateGizmoContourPanel()	// show and hide the other controls
	ContourNeedsUpdate(1)
End

Static Function ColorPlaneColorPopMenuProc(ctrlName,popNum,popStr) : PopupMenuControl
	String ctrlName
	Variable popNum
	String popStr

	String gizmoName,surfaceName,contourName
	GetPanelGizmoSurfaceAndContour(gizmoName,surfaceName,contourName)
	
	ControlInfo/W=GizmoContours $ctrlName
	
	Variable/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"planeRed")= V_red
	Variable/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"planeGreen")= V_green
	Variable/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"planeBlue")= V_blue

	ContourNeedsUpdate(1)
	UpdateGizmoContourPanel()
End

Static Function PlaneWhereRadioProc(planeWhere,checked) : CheckBoxControl
	String planeWhere	// "radioPlaneBelowContours" or "radioPlaneAboveContours"
	Variable checked

	CheckBox radioPlaneBelowContours, win= GizmoContours, value= CmpStr(planeWhere, "radioPlaneBelowContours") == 0
	CheckBox radioPlaneAboveContours, win= GizmoContours, value= CmpStr(planeWhere, "radioPlaneAboveContours") == 0
	
	String gizmoName,surfaceName,contourName
	GetPanelGizmoSurfaceAndContour(gizmoName,surfaceName,contourName)

	String/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"planeWhere")= planeWhere
	
	ContourNeedsUpdate(1)
End

Static Function CalcNormalsCheckProc(ctrlName,checked) : CheckBoxControl
	String ctrlName
	Variable checked
	
	String gizmoName,surfaceName,contourName
	GetPanelGizmoSurfaceAndContour(gizmoName,surfaceName,contourName)
	
	Variable/G $GizmoSurfaceContourDFVar(gizmoName,surfaceName,contourName,"calcNormals")= checked

	ContourNeedsUpdate(1)
End
